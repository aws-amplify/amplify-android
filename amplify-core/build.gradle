apply plugin: 'com.android.library'
apply plugin: 'devicefarm'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation "com.android.support:support-v4:28.0.0"
    implementation "com.android.support:support-annotations:28.0.0"

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
    implementation project(path: ':aws-amplify-api-okhttp')
}

devicefarm {

    // Required. The Project must already exist. You can create a project in the AWS console.
    projectName "AWSAndroidKeyStoreTester"

    // Required. You must specify either accessKey and secretKey OR roleArn. roleArn takes precedence.
    authentication {
        accessKey "$System.env.DEVICEFARM_ACCESS_KEY"
        secretKey "$System.env.DEVICEFARM_SECRET_KEY"
    }

    devicePool "AWSAndroidKeyStoreTesterPool"
}

task generateReference(type: Javadoc) {
    title = "Amplify for Android v${VERSION_NAME}"
    source = android.sourceSets.main.java.srcDirs
    ext.androidJar = "${android.sdkDirectory}/platforms/${android.compileSdkVersion}/android.jar"
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    options.links("https://developer.android.com/reference/")
    options.links("https://developers.google.com/android/reference/")
    options.links("https://aws.github.io/aws-sdk-android/docs/reference/")
    destinationDir = file("../docs/reference")

    failOnError = false
    options.showAll()
    options.addStringOption('Xdoclint:none', '-quiet')
}
