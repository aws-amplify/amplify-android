/*
 * Copyright 2019 Amazon.com, Inc. or its affiliates. All Rights Reserved.
 *
 * Licensed under the Apache License, Version 2.0 (the "License").
 * You may not use this file except in compliance with the License.
 * A copy of the License is located at
 *
 *  http://aws.amazon.com/apache2.0
 *
 * or in the "license" file accompanying this file. This file is distributed
 * on an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either
 * express or implied. See the License for the specific language governing
 * permissions and limitations under the License.
 */

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'
apply from: rootProject.file("configuration/checkstyle.gradle")
apply from: rootProject.file("configuration/android-lint.gradle")
apply from: rootProject.file('gradle-mvn-push.gradle')

android {
    compileSdkVersion 29

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"

        // Specifies the fully-qualified class name of the test instrumentation runner.
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    // Rx is part of our API signature for the DataStore category.  If a
    // consumer of Amplify will use the Rx-based DataStore APIs, RxJava2
    // must be available at runtime.
    //
    // As a corollary, a plugin should declare an `implementation`
    // dependency on Rx, so that it can implement the needed API:
    //
    //   implementation 'io.reactivex.rxjava2:rxjava:2.2.13'
    //
    // Without this, any use of Observable will raise a runtime
    // exception.
    compileOnly "io.reactivex.rxjava2:rxjava:$rxJava2Version"

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.annotation:annotation:1.1.0'

    testImplementation 'junit:junit:4.12'
    testImplementation('org.robolectric:robolectric:4.3.1') {
        // https://github.com/robolectric/robolectric/issues/5245
        exclude group: 'com.google.auto.service', module: 'auto-service'
    }
    testImplementation project(path: ':aws-testmodels')

    // Required for instrumented tests
    androidTestImplementation 'androidx.annotation:annotation:1.1.0'
    androidTestImplementation 'androidx.test:core:1.2.0' // Core Library
    androidTestImplementation 'androidx.test:runner:1.2.0' // Test Runner
    androidTestImplementation 'androidx.test.ext:junit:1.1.1' // Assertions
}

project.afterEvaluate {
    publishing {
        publications {
            library(MavenPublication) {
                groupId 'com.amplifyframework'
                artifactId 'core'
                version VERSION_NAME
                artifact(bundleReleaseAar)
            }
        }
    }
}
